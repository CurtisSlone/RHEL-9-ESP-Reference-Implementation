# Sudo Privileges Security Check - Explicit Validation Design
# Separates file identification from validation requirements

META
    version `1.0.0`
    esp_version `1.0`
    author `security-team`
    date `2024-01-15`
    severity `high`
    platform `linux`
    description `sudo`
    control_framework `NIST`
    control `AC-2.4`
    esp_scan_id `sudo-priv-test`
    criticality `high`
    tags `linux, file_content,file_metadata`
META_END


DEF

VAR sudoers_path string `scanfiles/sudoers`
VAR required_permissions string `0440`
VAR required_owner string `root`
VAR required_group string `root`

# File identification only - no implicit validation
OBJECT sudoers_file
    path VAR sudoers_path
    type `file`
OBJECT_END

# Explicit file system security posture validation
STATE file_security_posture
    permissions string = VAR required_permissions
    owner string = VAR required_owner
    group string = VAR required_group
    exists boolean = true
    readable boolean = true
STATE_END

# Explicit content policy validation
STATE logging_configuration
    content string contains `logfile=`
    content string contains `log_input,log_output`
STATE_END

STATE password_policy_configuration
    content string contains `passwd_tries`
    content string contains `timestamp_timeout`
STATE_END

STATE secure_path_configuration
    content string contains `secure_path=`
    content string contains `/usr/sbin`
    content string contains `/sbin`
STATE_END

STATE dangerous_permissions_absent
    content string not_contains `NOPASSWD: ALL`
    content string not_contains `ALL=(ALL) NOPASSWD:`
STATE_END

# Explicit file size validation (ensure file isn't empty or corrupted)
STATE file_integrity
    size int > 100
    size int < 50000
STATE_END

CRI OR
    # Check metadata first (fast operation)
    CTN file_metadata
        TEST all all
        STATE_REF file_security_posture
        OBJECT_REF sudoers_file
    CTN_END

    # Only check content if metadata passes (expensive operation)
    CTN file_content
        TEST all at_least_one OR
        STATE_REF logging_configuration
        STATE_REF password_policy_configuration
        STATE_REF secure_path_configuration
        STATE_REF dangerous_permissions_absent
        OBJECT_REF sudoers_file
    CTN_END
CRI_END

DEF_END
